apply plugin: 'com.android.application'
if (enable_plugin_kotlin) {
    apply plugin: 'kotlin-android'
    apply plugin: 'kotlin-android-extensions'
    apply plugin: 'kotlin-kapt'
}

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "io.github.lonamiwebs.stringlate"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1006
        versionName "0.15.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String[]", "DETECTED_ANDROID_LOCALES", "${findUsedAndroidLocales()}"
        buildConfigField "String", "BUILD_DATE", "\"${getBuildDate()}\""
        buildConfigField "String", "GITHASH", "\"${getGitHash()}\""
        resValue "string", "manifest_package_id", "io.github.lonamiwebs.stringlate"
    }

    flavorDimensions "default"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        if (enable_plugin_kotlin) {
            main.java.srcDirs += 'src/main/kotlin'
        }
    }

    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
}

ext {
    // https://developer.android.com/topic/libraries/support-library/revisions.html
    supportLib = '28.0.0'

    // https://github.com/JakeWharton/butterknife/releases
    butterknife = '8.8.1'

    // https://github.com/guardianproject/NetCipher/releases
    netcipher = '2.0.0-beta1'
}

dependencies {
    // Android standard libs
    implementation "com.android.support:design:${supportLib}"
    implementation "com.android.support:appcompat-v7:${supportLib}"
    implementation "com.android.support:preference-v7:${supportLib}"
    implementation "com.android.support:preference-v14:${supportLib}"
    // Tool libraries
    implementation "com.jakewharton:butterknife:${butterknife}"
    // Include core project
    implementation(project(':core')) {
        exclude(group: 'org.json', module: 'json')
        exclude(group: 'org.apache.servicemix.bundles', module: 'org.apache.servicemix.bundles.xmlpull')
    }

    // Processors
    def anpros = ["com.jakewharton:butterknife-compiler:${butterknife}"]
    for (anpro in anpros) {
        if (enable_plugin_kotlin) {
            kapt anpro
        } else {
            annotationProcessor anpro
        }
    }
}
